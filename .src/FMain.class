' Gambas class file

Public flotaActual As Flota
Public tablerojuego As New Tablero

Public JugadorUno As String = "Jugador 1"

Public paso As Integer = 0 'variable bandera que indica si hemos seleccionado antes el planeta o no.

Private Observador As Observer

Private pinicial As Planeta 'planeta inicial que se hace click

Public Sub Form_Open()

  Randomize

  TextAreaInformacion.text = ("Construye tu Imperio Galáctico...") & "\n" & ("Tu misión: Explora y domina el mayor número de planetas...") & finlinea()

  Me.x = 100 'posicion del formulario de juego

  'creo los planetas que se mostraran.
  tablerojuego.crearPlanestas()

  'creo planeta del jugador
  tablerojuego.creoPlanetaJugador()

  'inicio relojes
  TimerActualizarPosiciones.Start()
  TimerProduccion.Start()
  TimerFlotasEnemigas.Start()

End

Private Function finlinea() As String

  TextAreaInformacion.Pos = TextAreaInformacion.Length
  Return "\n"

End

Public Sub DrawingArea1_MouseDown()

  Dim p As Planeta
  'hago clip....

  p = tablerojuego.planetaCercano(Mouse.x, Mouse.y)

  '  Print p.Produccion, p.Propietario

  If p.Propietario = JugadorUno Then
    seleccion(p)
  Else

    If paso = 1 Then
      flotaActual.destino = p
      flotaActual.activa = True
      tablerojuego.ArrayFlotas.Add(flotaActual)
      paso = 0
      pinicial.naves -= flotaActual.naves

      TextAreaInformacion.text &= ("Flota enviada a la conquista del planeta enemigo...") & p.escribe()
      TextAreaInformacion.text &= finlinea()
      'actualizo dibujo
      DrawingArea1.Refresh()
    Else
      'escribo datos del planeta
      TextAreaInformacion.text &= ("Nombre:") & " " & p.nombre & "\t"
      TextAreaInformacion.text &= ("Produccion:") & " " & p.Produccion & "\t"
      TextAreaInformacion.text &= ("Naves:") & " " & p.Naves
      TextAreaInformacion.text &= finlinea()
    Endif
  Endif

End

Public Sub seleccion(p As Planeta)

  Dim res As Integer
  Dim v As Integer
  Dim Arraynumero As New Integer[]

  If paso = 0 Then
    'seleccion primera
    If p.naves = 0 Then

      Message.Info(("No hay naves para enviar a ningún lado"))
      Return
    Endif

    If Int(p.naves / 2) = 0 Then
      Message.Info(("No hay naves bastantes para enviar a ningún lado"))
      Return
    Endif

    FormCreandoFlota.numero = Arraynumero 'sirve para obtener el numero de naves elegidas en el formulario FormCreandoFlota
    FormCreandoFlota.valormaximo = p.naves
    FormCreandoFlota.xx = Me.x + p.Posicion.X
    FormCreandoFlota.yy = Me.y + p.Posicion.Y

    FormCreandoFlota.NavesDisponibles = p.naves
    FormCreandoFlota.nombrePlaneta = p.nombre

    FormCreandoFlota.Showmodal()
    res = Arraynumero[0]

    If res = -1 Or res = 0 Then Return

    TextAreaInformacion.text &= ("Elija planeta de destino...")
    TextAreaInformacion.text &= finlinea()

    flotaActual = New Flota
    flotaActual.Propietario = JugadorUno
    Object.Attach(flotaActual, Me, "obs") 'porque lo estamos usando dentro de un formulario

    flotaActual.origen = p
    flotaActual.naves = res
    pinicial = p
    paso = 1
  Else

    If p.igual(pinicial) Then
      Message.Info("has hecho click en el mismo planeta")
      paso = 0
      flotaActual = Null

      Return
    Endif

    'enviar naves a un planeta amigo..

    flotaActual.destino = p
    flotaActual.activa = True
    tablerojuego.ArrayFlotas.Add(flotaActual)
    paso = 0
    pinicial.naves -= flotaActual.naves
    TextAreaInformacion.text &= ("Flota enviada al planeta amigo") & " " & p.escribe()
    TextAreaInformacion.text &= finlinea()

  Endif

End

Public Sub TimerActualizarPosiciones_Timer()

  tablerojuego.ActualizarPosicionFlotas()

  DrawingArea1.Refresh()

End

Public Sub obs_llegada(p As Planeta, f As Flota)

  If p.Propietario = f.Propietario Then
    'las flotas se suman
    p.naves += f.naves

    '-----------------------------------------------
    'texto para identificar flota
    If f.Propietario = "N/A" Then
      TextAreaInformacion.text &= ("El imperio reune sus fuerzas..")
    Endif
    '-----------------------------------------------

    TextAreaInformacion.text &= ("Llegada de la flota al planeta") & " " & p.escribe() & ("... reuniendo naves para el próximo ataque")
    TextAreaInformacion.text &= finlinea()

  Else
    'es un planeta enemigo:
    p.naves -= f.naves

    '-----------------------------------------------
    'texto para identificar flota
    If f.Propietario = "N/A" Then
      TextAreaInformacion.text &= ("Imperio: ")
    Else
      TextAreaInformacion.text &= ("Alianza Rebelde: ")
    Endif
    '-----------------------------------------------

    If p.naves < 0 Then
      'cambia de dueño el planeta
      p.naves = Abs(p.naves)
      p.Propietario = f.Propietario
      TextAreaInformacion.text &= ("Victoria!!!!.. planeta") & " " & p.escribe() & ("conquistado.")
      TextAreaInformacion.text &= finlinea()

    Else
      TextAreaInformacion.text &= ("Derrota!!!!.. el planeta") & " " & p.escribe() & ("ha resistido el ataque.")
      TextAreaInformacion.text &= finlinea()
    Endif

  Endif

  'borro la flota del array
  tablerojuego.ArrayFlotas.Delete(tablerojuego.ArrayFlotas.Find(f))
  f = Null

End

Public Sub TimerProduccion_Timer()

  tablerojuego.ActualizarProduccionPlanetas()

  TextAreaInformacion.text &= ("Fin de turno de Producción") & finlinea()

  DrawingArea1.Refresh()

End

Public Sub TimerFlotasEnemigas_Timer()

  Dim flotatmp As Flota
  Dim flotaImperialTmp As Flota[]

  'agrega flotas imperiales al tablero

  flotaImperialTmp = IA.creaflota(tablerojuego.ArrayPlanetas) ''devuelve un array de flotas imperiales

  If IsNull(flotaImperialTmp) Then
    'Si no se ha podido crear las flotas imperiales, es por dos causas:
    ' O No hay planetas de la rebelion
    ' O No hay planetas del imperio
    'por lo tanto hay que mostrar el infomefinal del juego

    'paro los timer
    TimerActualizarPosiciones.Stop
    TimerFlotasEnemigas.Stop
    TimerProduccion.Stop

    'comprobar condiciones del final de juego
    tablerojuego.InformeFinalJuego()
    Me.Close()
    Return
  Endif

  For Each flotatmp In flotaImperialTmp
    Object.Attach(flotatmp, Me, "obs") ' las observo, para cuando lleguen a un planeta ver su evento.
  Next

  tablerojuego.ArrayFlotas.insert(flotaImperialTmp) 'las añado al tablero

End

'----------------------------------------
'dibujar el tablero
'----------------------------------------

Public Sub DrawingArea1_Draw()

  tablerojuego.DibujaTablero()

End
