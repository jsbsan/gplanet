' Gambas class file

Event NuevoMensaje(texto As String) 'evento que manda el tablero, con el mensaje que queremos que se muestre en el formulario

Public listaNombrePlanetas As String[]
Public ArrayPlanetas As New Planeta[]

Public ArrayFlotas As New Flota[]

Public celda As New Integer[40, 24] 'cuadricula donde se colocaran los planetas

Private fondotmp As New Fondo

Private ObservadorDeFlotas As Observer 'observa las flotas

Public Sub _new()
  'carga nombre de lista de planetas

  listaNombrePlanetas = Split(File.Load("planetas.txt"), "\n")

End

Public Function casillalibre() As Point 'devuelve una coordenada X, Y donde no exista ningun planeta.

  Dim x, y As Integer
  Dim casilla As New Point

  While True
    x = Int(Rnd(3, 38))
    y = Int(Rnd(3, 22))
    If celda[x, y] = 0 And alrededor(x, y) = True Then
      celda[x, y] = 1
      casilla.x = x
      casilla.y = y
      Return casilla
    Endif

  Wend

End

Private Function alrededor(vx As Integer, vy As Integer) As Boolean 'Comprueba que alrededor de la coordenada que nos pasan, no hay planetas. Asi evitamos que esten muy juntos los planetas.

  Dim a As Integer

  For a = 0 To 4
    If celda[vx - 2 + a, vy - 1] <> 0 Then Return False
    If celda[vx - 2 + a, vy + 1] <> 0 Then Return False
  Next
  If celda[vx - 2, vy] <> 0 Then Return False
  If celda[vx - 1, vy] <> 0 Then Return False
  If celda[vx + 1, vy] <> 0 Then Return False
  If celda[vx + 2, vy] <> 0 Then Return False
  Return True

End

Public Sub crearPlanestas()

  Dim a As Integer
  Dim p As Planeta

  Dim n As String

  For a = 1 To 10 'creo 10 planetas
    p = New Planeta("N/A", Me) 'asigno nuevo planeta al imperio

    n = listaNombrePlanetas.Extract(Int(Rnd(1, listaNombrePlanetas.Count)))[0] 'extraigo aleatoriamente un nombre de la lista de planetas
    p.nombre = n 'asigno nombre
    p.naves = Int(Rnd(1, 6)) 'asigno numero de naves
    ArrayPlanetas.Add(p) 'lo a침ado al Array de Plantetas.
  Next

End

Public Sub creoPlanetaJugador()

  Dim p As Planeta

  p = New Planeta("Jugador 1", Me)
  P.naves = MGlobales.NavesInicialesJugador
  p.nombre = listaNombrePlanetas[0]
  ArrayPlanetas.Add(p)

End

Public Function planetaCercano(x As Integer, y As Integer) As Planeta 'devuelve el planeta m치s cercado dada unas coordenadas

  Dim ptmp As Planeta
  Dim distanciaMinima As Integer = 1000000
  Dim disttmp As Integer
  Dim pCercano As Planeta

  For Each ptmp In ArrayPlanetas
    disttmp = ptmp.distancia(x, y)
    If distanciaMinima > ptmp.distancia(x, y) Then
      distanciaMinima = disttmp
      pCercano = ptmp
    Endif
  Next

  Return pCercano

End

Public Sub ActualizarPosicionFlotas()

  Dim flotatmp As Flota
  'hace que avancen las flotas
  For Each flotatmp In Me.ArrayFlotas
    flotatmp.avance()
  Next

End

Public Sub ActualizarProduccionPlanetas()

  Dim ptmp As Planeta
  'hace que todos los planetas fabriquen naves!!!
  For Each ptmp In ArrayPlanetas
    ptmp.naves += ptmp.Produccion
  Next

End

Public Sub DibujaTablero()

  Dim flotatmp As Flota
  Dim planetTmp As Planeta

  'dibujo fondo
  fondotmp.dibujo()
  'dibujo naves navegando
  For Each flotatmp In ArrayFlotas
    flotatmp.dibuja()
  Next

  'dibujo planetas y datos
  For Each planetTmp In ArrayPlanetas
    planetTmp.dibuja()
  Next

End

'Condiciones del final de juego...
Public Sub InformeFinalJuego()

  Dim planetaTmp As Planeta
  Dim flotatmp As Flota

  Dim ContadorImperialPlanetas As Integer
  Dim ContadorImperialNavesEnPlanetas As Integer
  Dim ContadorImperialNavesEnFlotas As Integer

  Dim ContadorJugadorPlanetas As Integer
  Dim ContadorJugadorNavesEnPlanetas As Integer
  Dim ContadorJugadorNavesEnFlotas As Integer

  For Each planetaTmp In ArrayPlanetas
    If planetaTmp.Propietario = "N/A" Then
      ContadorImperialPlanetas += 1
      ContadorImperialNavesEnPlanetas += planetaTmp.naves
    Else
      ContadorJugadorPlanetas += 1
      ContadorJugadorNavesEnPlanetas += planetaTmp.naves
    Endif
  Next

  For Each flotatmp In ArrayFlotas
    If flotatmp.Propietario = "N/A" Then

      ContadorImperialNavesEnFlotas += flotatmp.naves
    Else

      ContadorJugadorNavesEnFlotas += flotatmp.naves
    Endif

  Next

  FormInforme.RebeldesNaves = ContadorJugadorNavesEnFlotas + ContadorJugadorNavesEnPlanetas
  FormInforme.RebeldesPlanetas = ContadorJugadorPlanetas
  FormInforme.ImperioNaves = ContadorImperialNavesEnFlotas + ContadorImperialNavesEnPlanetas
  FormInforme.ImperioPlanetas = ContadorImperialPlanetas

  FormInforme.Showmodal()

End
'-----------------------------
'observar las flotas
'-----------------------------

Public Sub ObservarArrayFlota(ArrayFlota As Flota[])

  Dim flotatmp As Flota

  For Each flotatmp In ArrayFlota
    ObservarFlota(flotatmp)
  Next

End

Public Sub ObservarFlota(flotatmp As Flota)

  ObservadorDeFlotas = New Observer(flotatmp) As "ObservadorDeFlotas" 'las observo para cuando lleguen a un planeta ver su evento "llegada"

End

Public Sub ObservadorDeFlotas_llegada(p As Planeta, f As Flota)

  If p.Propietario = f.Propietario Then
    'las flotas se suman
    p.naves += f.naves

    '-----------------------------------------------
    'texto para identificar flota
    If f.Propietario = "N/A" Then
      Raise NuevoMensaje(("El imperio reune sus fuerzas.."))
    Endif
    '-----------------------------------------------

    Raise NuevoMensaje(("Llegada de la flota al planeta") & " " & p.escribe() & ("... reuniendo naves para el pr칩ximo ataque"))

  Else
    'es un planeta enemigo:
    p.naves -= f.naves

    '-----------------------------------------------
    'texto para identificar flota
    If f.Propietario = "N/A" Then
      Raise NuevoMensaje(("Imperio: "))
    Else
      Raise NuevoMensaje(("Alianza Rebelde: "))
    Endif
    '-----------------------------------------------

    If p.naves < 0 Then
      'cambia de due침o el planeta
      p.naves = Abs(p.naves)
      p.Propietario = f.Propietario
      Raise NuevoMensaje(("Victoria!!!!.. planeta") & " " & p.escribe() & ("conquistado."))

    Else
      Raise NuevoMensaje(("Derrota!!!!.. el planeta") & " " & p.escribe() & ("ha resistido el ataque."))

    Endif

  Endif

  'borro la flota del array
  ArrayFlotas.Delete(ArrayFlotas.Find(f))
  f = Null

End
